plugins {
	id 'java'
	id 'maven-publish'
	// https://plugins.gradle.org/plugin/com.jfrog.bintray
	id 'com.jfrog.bintray' version '1.8.5'
	// https://plugins.gradle.org/plugin/de.clashsoft.simple-publish
	id 'de.clashsoft.simple-publish' version '0.6.0'
	// https://plugins.gradle.org/plugin/org.fulib.fulibGradle
	id 'org.fulib.fulibGradle' version '0.4.0'
}

// --------------- Info ---------------

group = 'org.fulib'
version = "git describe --tags".execute().text[1..-2] // strip v and \n
description = 'Tools for generating HTML mockups and Web Apps.'

publishInfo {
	websiteUrl = 'https://www.fulib.org/'
	githubRepo = 'fujaba/fulibMockups'
	labels = [ 'source', 'generator', 'html', 'mockups', 'webapp' ]

	license {
		name = 'BSD 3-Clause'
		url = 'https://opensource.org/licenses/BSD-3-Clause'
	}

	developer {
		id = 'Clashsoft'
		name = 'Adrian Kunz'
		email = 'clashsoft@hotmail.com'
	}

	developer {
		id = 'azuendorf'
		name = 'Albert ZÃ¼ndorf'
		email = 'zuendorf@uni-kassel.de'
	}
}

sourceCompatibility = 1.8

// --------------- Dependencies ---------------

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

dependencies {
	// https://mvnrepository.com/artifact/org.fulib/fulibScenarios
	fulibScenarios group: 'org.fulib', name: 'fulibScenarios', version: '1.3.0'

	// https://mvnrepository.com/artifact/org.fulib/fulibYaml
	implementation group: 'org.fulib', name: 'fulibYaml', version: '1.1.0'

	// https://mvnrepository.com/artifact/org.fulib/fulibTools
	testCompile group: 'org.fulib', name: 'fulibTools', version: '1.1.0'

	// https://mvnrepository.com/artifact/junit/junit
	testCompile group: 'junit', name: 'junit', version: '4.12'
}

// --------------- FulibGradle Overrides ---------------

// generate classes into build/generated-src instead of src/main/java

final File modelDir = file "$project.buildDir/generated/sources/scenarios/main/"
final File testDir = file "$project.buildDir/generated/sources/scenarios/test/"

sourceSets.main.java.srcDir(modelDir)
sourceSets.test.java.srcDir(testDir)

generateScenarioSource {
	modelDirectory = modelDir
	testDirectory = testDir
	classDiagramSVG = true
}

generateTestScenarioSource {
	modelDirectory = testDir
	testDirectory = testDir
}
