plugins {
	id 'java'
	id 'maven-publish'
	id 'com.jfrog.bintray' version '1.8.4'
	id 'de.clashsoft.simple-publish' version '0.2.0'
	id 'org.fulib.fulibGradle' version '0.2.0'
}

// --------------- Info ---------------

group = 'org.fulib'
version = "git describe --tags".execute().text[1..-2] // strip v and \n
description = 'Tools for generating HTML org.fulib.mockups and Web Apps.'

publishInfo {
	websiteUrl = 'https://www.fulib.org/'
	issueTrackerUrl = 'https://github.com/fujaba/fulibMockups'
	vcsUrl = 'https://github.com/fujaba/fulibMockups'
	githubRepo = 'fujaba/fulibMockups'
	labels = [ 'source', 'generator', 'html', 'org.fulib.mockups', 'webapp' ]

	license {
		shortName = 'BSD 3-Clause'
		longName = 'BSD 3-Clause "New" or "Revised" License'
		url = 'https://opensource.org/licenses/BSD-3-Clause'
	}

	developer {
		id = 'Clashsoft'
		name = 'Adrian Kunz'
		email = 'clashsoft@hotmail.com'
	}

	developer {
		id = 'azuendorf'
		name = 'Albert ZÃ¼ndorf'
		email = 'zuendorf@uni-kassel.de'
	}
}

sourceCompatibility = 1.8

// --------------- Dependencies ---------------

repositories {
	mavenLocal()
	mavenCentral()
	jcenter()
}

dependencies {
	// https://mvnrepository.com/artifact/org.fulib/fulibScenarios
	fulibScenarios group: 'org.fulib', name: 'fulibScenarios', version: '1.0.0'

	// https://mvnrepository.com/artifact/org.fulib/fulibYaml
	implementation group: 'org.fulib', name: 'fulibYaml', version: '1.1.0'

	// https://mvnrepository.com/artifact/org.fulib/fulibTools
	testCompile group: 'org.fulib', name: 'fulibTools', version: '1.1.0'

	// https://mvnrepository.com/artifact/junit/junit
	testCompile group: 'junit', name: 'junit', version: '4.12'
}

// --------------- FulibGradle Overrides ---------------

// generate classes into build/generated-src instead of src/main/java

final File modelDir = file "src/gen/java/"
final File testDir = file "src/test/java/"

sourceSets.main.java.srcDir(modelDir)
sourceSets.test.java.srcDir(testDir)

generateScenarioSource {
	modelDirectory = modelDir
	testDirectory = testDir
	classDiagramSVG = true
}

generateTestScenarioSource {
	modelDirectory = testDir
	testDirectory = testDir
}
